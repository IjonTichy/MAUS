actor "Basic Shotgun": Weapon replaces Shotgun
{
    Weapon.AmmoUse 1
    Weapon.AmmoGive 8
    Weapon.AmmoType "Shell"

    Weapon.SlotNumber 3
    Weapon.Kickback 100

    Inventory.PickupMessage "$GOTSHOTGUN" // "You got the shotgun!"
    Obituary "$OB_MPSHOTGUN" // "%o chewed on %k's boomstick."

    States
    {
      Ready:
        SHTG A 1 A_WeaponReady
        Loop

      Deselect:
        SHTG A 1 A_Lower
        Loop

      Select:
        SHTG A 1 A_Raise
        Loop

      Fire:
        SHTG B 0 A_FireBullets(ACS_ExecuteWithResult($DOOMRL_ACCURACY, 7,00,2)/100.0, ACS_ExecuteWithResult($DOOMRL_ACCURACY, 2,00,2)/100.0, 7, ACS_ExecuteWithResult($DOOMRL_DAMAGE, 5,2,0) )
        SHTG A 0 A_PlaySound ("weapons/shotgf", CHAN_WEAPON)
        SHTG A 2 A_GunFlash
        SHTG A 0 A_JumpIf(ACS_ExecuteWithResult($DOOMRL_GETSTAT, $STAT_SHOTTYMAN,0,0) >= 3, "Pump.SM3")
        SHTG A 0 A_JumpIf(ACS_ExecuteWithResult($DOOMRL_GETSTAT, $STAT_SHOTTYMAN,0,0) == 2, "Pump.SM2")
        SHTG A 0 A_JumpIf(ACS_ExecuteWithResult($DOOMRL_GETSTAT, $STAT_SHOTTYMAN,0,0) == 1, "Pump.SM1")
        goto Pump.SM0

      Pump.SM0:
        SHTG A 11
        SHTG BC 6
        SHTG D  7
        SHTG CB 6
        SHTG A 4
        SHTG A 0 A_ReFire
        Goto Ready

      Pump.SM1:
        SHTG A 8
        SHTG BC 5
        SHTG D  6
        SHTG CB 5
        SHTG A 4
        SHTG A 0 A_ReFire
        goto Ready

      Pump.SM2:
        SHTG A 5
        SHTG BC 4
        SHTG D  4
        SHTG CB 3
        SHTG A 4
        SHTG A 0 A_ReFire
        goto Ready

      Pump.SM3:
        SHTG A 3
        SHTG BC 3
        SHTG D  3
        SHTG CB 2
        SHTG A 4
        SHTG A 0 A_ReFire
        goto Ready


      Flash:
        SHTF A 4 Bright A_Light1
        SHTF B 3 Bright A_Light2
        Goto LightDone
      Spawn:
        SHOT A -1
        Stop
    }
}
