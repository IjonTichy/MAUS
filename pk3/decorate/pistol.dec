actor "Basic Pistol": Weapon replaces Pistol
{
    Weapon.AmmoUse 1
    Weapon.AmmoGive 20
    Weapon.AmmoType "Clip"

    Weapon.SlotNumber 2

    Obituary "$OB_MPPISTOL" // "%o was tickled by %k's pea shooter."
    Inventory.Pickupmessage "$PICKUP_PISTOL_DROPPED" // "Picked up a pistol."

    Decal BulletChip

    States
    {
      Ready:
        PISG A 1 A_WeaponReady
        Loop

      Deselect:
        PISG A 1 A_Lower
        Loop

      Select:
        PISG A 1 A_Raise
        Loop

      Fire:
        PISG B 0 A_FireBullets(ACS_ExecuteWithResult($DOOMRL_ACCURACY, 3,00,1)/100.0, ACS_ExecuteWithResult($DOOMRL_ACCURACY, 3,00,1)/100.0, 1, ACS_ExecuteWithResult($DOOMRL_DAMAGE, 5,1,0) )
        PISG B 0 A_PlaySound("weapons/pistol", CHAN_WEAPON)
        PISG B 0 A_GunFlash
        PISG B 0 A_JumpIf(ACS_ExecuteWithResult($DOOMRL_GETSTAT, $STAT_SONOFAGUN,0,0) == 1, "Fire.SoaG1")
        PISG B 0 A_JumpIf(ACS_ExecuteWithResult($DOOMRL_GETSTAT, $STAT_SONOFAGUN,0,0) == 2, "Fire.SoaG2")
        PISG B 0 A_JumpIf(ACS_ExecuteWithResult($DOOMRL_GETSTAT, $STAT_SONOFAGUN,0,0) >= 3, "Fire.SoaG3")
        goto Fire.SoaG0

      Fire.SoaG0:
        PISG B 3
        PISG C 6
        PISG B 5
        PISG A 4
        PISG A 0 A_ReFire
        goto Ready
      
      Fire.SoaG1:
        PISG B 3
        PISG C 5
        PISG B 4
        PISG A 2
        PISG A 0 A_ReFire
        goto Ready
      
      Fire.SoaG2:
        PISG B 3
        PISG C 3
        PISG B 3
        PISG A 1
        PISG A 0 A_ReFire
        goto Ready
      
      Fire.SoaG3:
        PISG B 1
        PISG C 3
        PISG B 2
        PISG A 1
        PISG A 0 A_ReFire
        goto Ready

      Flash:
        PISF A 3 Bright A_Light1
        Goto LightDone
        PISF A 3 Bright A_Light0
        Goto LightDone
      Spawn:
        PIST A -1
        Stop
    }
}
